plugins {
    id 'distroboy.common'
    id 'com.google.protobuf'
    id 'idea'
    id 'java-library'
}

repositories {
    mavenCentral()
}

dependencies {
    api group: 'com.google.protobuf', name: 'protobuf-java', version: '3.19.1'

    api platform(group: 'io.grpc', name: 'grpc-bom', version: '1.43.1')
    api (group: 'io.grpc', name: 'grpc-protobuf')
    api (group: 'io.grpc', name: 'grpc-stub')
    api (group: 'io.grpc', name: 'grpc-netty')
    api group: 'io.perfmark', name: 'perfmark-api', version: '0.24.0'

    // Java 9+ requires this to be here for GRPC to compile properly
    // https://github.com/grpc/grpc-java/issues/3633
    implementation group: 'javax.annotation', name: 'javax.annotation-api', version: '1.3.2'
}

sourceSets {
    main {
        proto {
            include 'google/**/*.proto'
        }
    }
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.19.1"
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.43.1'
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {
            }
        }
    }
    generatedFilesBaseDir = "$projectDir/gen"
}

clean {
    delete protobuf.generatedFilesBaseDir
}

idea {
    module {
        // proto files and generated Java files are automatically added as
        // source dirs.
        // If you have additional sources, add them here:
//    sourceDirs += file("$projectDir/something/else/");
    }
}